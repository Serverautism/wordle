plugins {
    id 'java'
    id 'application'
}

description = 'Wordle'
group = 'org.example'
version = 'unspecified'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation "org.jmonkeyengine:jme3-core:3.3.2-stable"
    implementation "org.jmonkeyengine:jme3-desktop:3.3.2-stable"
    implementation "org.jmonkeyengine:jme3-lwjgl3:3.3.2-stable"
    implementation "org.jmonkeyengine:jme3-networking:3.3.2-stable"

    implementation 'com.google.code.gson:gson:2.11.0'
}

application {
    mainClass = 'main.client.WordleApp'
    applicationName = 'Wordle'
}

test {
    useJUnitPlatform()
}

jar {
    enabled = false
}

tasks.register('clientJar', Jar) {
    group = "build"
    description = "client build without server code"

    from(sourceSets.main.output) {
        include 'client/**'
        include 'model/client/**'
        include 'model/general/**'
    }

    archiveBaseName = "Wordle-Client"

    manifest {
        attributes(
                'Main-Class': 'client.WordleApp'
        )
    }
}

tasks.register('serverJar', Jar) {
    group = "build"
    description = "server build without client code"

    from(sourceSets.main.output) {
        include 'server/**'
        include 'model/server/**'
        include 'model/general/**'
    }

    archiveBaseName = "Wordle-Server"

    manifest {
        attributes(
                'Main-Class': 'server.WordleServer'
        )
    }
}

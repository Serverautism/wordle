plugins {
    id 'java'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    implementation "org.jmonkeyengine:jme3-core:3.3.2-stable"
    implementation "org.jmonkeyengine:jme3-desktop:3.3.2-stable"
    implementation "org.jmonkeyengine:jme3-lwjgl3:3.3.2-stable"
    implementation "org.jmonkeyengine:jme3-networking:3.3.2-stable"

    implementation 'com.google.code.gson:gson:2.11.0'

    // implementation "org.jmonkeyengine:jme3-bullet:3.3.2-stable"
    // implementation "org.jmonkeyengine:jme3-jogg:3.3.2-stable"
    // implementation "org.jmonkeyengine:jme3-plugins:3.3.2-stable"
}

test {
    useJUnitPlatform()
}

jar {
    enabled = false
}

tasks.register('clientJar', Jar) {
    group = "build"
    description = "client build without server code"

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    from(sourceSets.main.output) {
        include 'client/**'
        include 'model/client/**'
        include 'model/general/**'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    archiveBaseName = "Wordle-Client"
    archiveVersion = "1.0"

    manifest {
        attributes(
                'Main-Class': 'client.WordleApp'
        )
    }
}

tasks.register('serverJar', Jar) {
    group = "build"
    description = "server build without client code"

    from(sourceSets.main.output) {
        include 'server/**'
        include 'model/server/**'
        include 'model/general/**'
    }

    archiveBaseName = "Wordle-Server"

    manifest {
        attributes(
                'Main-Class': 'server.WordleServer'
        )
    }
}